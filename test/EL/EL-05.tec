! TECO test: Open log file and verify data !
! Commands: 0:@EL// !
! Requirements: None !
! Execution: Redirect !
! Expect: PASS !

! Include: setup-01.tec !

@^UA|/tmp/teco.log|         ! Name of log file we create !
@^UB|EL-05.log|             ! Name of log file we compare against !

0:@EL/^EQA/                 ! Test: open log file !

"U
    @^A/Can't open log file/
    @^A/FAIL/
    ^C
'
``

! Logging starts here !

@^A/hello, world!/         ! Generate an output message !
``
5 < @I/abcdef/ 10I >        ! Insert some lines of text !
0 J                         ! Go to beginning of buffer !
3 L                         ! Move down 3 lines !
1 K                         ! And kill the next line !

! Logging ends here !
``

@EL//                       ! Test: close log file !
``

HK

:@EZ/diff -B -w -q ^EQA ^EQB/

"U
    @^A/Can't compare log files/
    @^A/FAIL/
    ^C
'

G+ 0J

:@S/differ/

"U
    @^A/Log files differ/
    @^A/FAIL/
    ^C
'

! Include: cleanup-01.tec !
